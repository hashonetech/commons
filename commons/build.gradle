plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

group = 'com.hashone.commons'
version = '1.0.2'

android {
    namespace 'com.hashone.commons'
    compileSdk 33

    defaultConfig {
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0.2"

        consumerProguardFiles "consumer-rules.pro"
    }

    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "${archivesBaseName}_${variant.name}_${defaultConfig.versionName}.aar"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {
    api 'androidx.core:core-ktx:1.10.0'
    api 'androidx.appcompat:appcompat:1.6.1'
    api 'com.google.android.material:material:1.8.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'

    /*Glide*/
    api 'com.github.bumptech.glide:glide:4.15.0'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.hashone'
                artifactId = 'commons'
                version = '1.0.2'
            }
        }
    }
}
